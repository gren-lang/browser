effect module Browser.AnimationManager where { subscription = MySub } exposing
    ( onAnimationFrame
    , onAnimationFrameDelta
    )

import Gren.Kernel.Browser
import Process
import Task exposing (Task)
import Time



-- PUBLIC STUFF


onAnimationFrame : (Time.Posix -> msg) -> Sub msg
onAnimationFrame tagger =
    subscription (Time tagger)


onAnimationFrameDelta : (Float -> msg) -> Sub msg
onAnimationFrameDelta tagger =
    subscription (Delta tagger)



-- SUBSCRIPTIONS


type MySub msg
    = Time (Time.Posix -> msg)
    | Delta (Float -> msg)


subMap : (a -> b) -> MySub a -> MySub b
subMap func sub =
    when sub is
        Time tagger ->
            Time (func << tagger)

        Delta tagger ->
            Delta (func << tagger)



-- EFFECT MANAGER


type alias State msg =
    { subs : Array (MySub msg)
    , request : Maybe Process.Id
    , oldTime : Int
    }



-- NOTE: used in onEffects
--


init : Task Never (State msg)
init =
    Task.succeed { subs = [], request = Nothing, oldTime = 0 }


onEffects : Platform.Router msg Int -> Array (MySub msg) -> State msg -> Task Never (State msg)
onEffects router subs { request, oldTime } =
    when { r = request, s = subs } is
        { r = Nothing, s = [] } ->
            init

        { r = Just pid, s = [] } ->
            Process.kill pid
                |> Task.andThen (\_ -> init)

        { r = Nothing, s = _ } ->
            Process.spawn (Task.andThen (Platform.sendToSelf router) rAF)
                |> Task.andThen
                    (\pid ->
                        now
                            |> Task.andThen (\time -> Task.succeed { subs = subs, request = (Just pid), oldTime = time })
                    )

        { r = Just _, s = _ } ->
            Task.succeed { subs = subs, request = request, oldTime = oldTime }


onSelfMsg : Platform.Router msg Int -> Int -> State msg -> Task Never (State msg)
onSelfMsg router newTime { subs, oldTime } =
    let
        send sub =
            when sub is
                Time tagger ->
                    Platform.sendToApp router (tagger (Time.millisToPosix newTime))

                Delta tagger ->
                    Platform.sendToApp router (tagger (toFloat (newTime - oldTime)))
    in
    Process.spawn (Task.andThen (Platform.sendToSelf router) rAF)
        |> Task.andThen
            (\pid ->
                Task.sequence (Array.map send subs)
                    |> Task.andThen (\_ -> Task.succeed { subs = subs, request = (Just pid), oldTime = newTime })
            )


rAF : Task x Int
rAF =
    Gren.Kernel.Browser.rAF {}


now : Task x Int
now =
    Gren.Kernel.Browser.now {}
